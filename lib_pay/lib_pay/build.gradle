plugins {
    id 'com.android.library'
}
apply plugin: 'com.alibaba.arouter'

android {
    compileSdkVersion       androidConfig.compileSdkVersion
    buildToolsVersion       androidConfig.buildToolsVersion

    defaultConfig {

        minSdkVersion       androidConfig.minSdkVersion
        targetSdkVersion    androidConfig.targetSdkVersion
        versionCode         androidConfig.versionCode
        versionName         androidConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    dependenciesConfig.each { k, v -> implementation v}

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //ARouter引用
    compile 'com.alibaba:arouter-api:1.5.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.1'

    api project(path: ':lib_pay:lib_pay_core')

    //库组件的动态配置
//    println("startParameter "+project.gradle.startParameter.toString())
//    println("taskNames "+project.gradle.startParameter.taskNames.toString())
    boolean handled = false
    for (String task : project.gradle.startParameter.taskNames) {
        task = task.toLowerCase()
        for (def config : libPayConfig){
            if(task.contains(config.key)){
                handled = true
                for (def v : config.value){
                    implementation project(path: v)
                }
                break
            }
        }
    }
    if(!handled){
        for (def v : libPayConfig.defaultCfg){
            implementation project(path: v)
        }
    }

}