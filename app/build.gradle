plugins {
    id 'com.android.application'
}
apply from: 'build_flavor.gradle'

static def buildTime() {
    return new Date().format("yyyy.MM.dd-HH:mm:ss")
}
android {
    compileSdkVersion       androidConfig.compileSdkVersion
    buildToolsVersion       androidConfig.buildToolsVersion

    defaultConfig {

        applicationId       applicationIdConfig.app

        minSdkVersion       androidConfig.minSdkVersion
        targetSdkVersion    androidConfig.targetSdkVersion
        versionCode         androidConfig.versionCode
        versionName         androidConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        pickFirst 'lib/x86/libsignature.so'
        pickFirst 'lib/armeabi-v7a/libsignature.so'

//            merge 'lib/x86/libsignature.so'
//            merge 'lib/armeabi-v7a/libsignature.so'

//          exclude 'lib/x86/libsignature.so'
//          exclude 'lib/armeabi-v7a/libsignature.so'
    }

    signingConfigs {

//        signConfig.each {k,v ->
//            println(k + " : ")
//            v.each {k1,v1 ->
//                println(" "+k1 + " " + v1)
//            }
//        }

        debug{
            keyAlias      signConfig.debug.keyAlias
            keyPassword   signConfig.debug.keyPassword
            storeFile     file(signConfig.debug.storeFile)
            storePassword signConfig.debug.storePassword
        }

        release{
            keyAlias      signConfig.release.keyAlias
            keyPassword   signConfig.release.keyPassword
            storeFile     file(signConfig.release.storeFile)
            storePassword signConfig.release.storePassword
        }

        release2{
            keyAlias      signConfig.release2.keyAlias
            keyPassword   signConfig.release2.keyPassword
            storeFile     file(signConfig.release2.storeFile)
            storePassword signConfig.release2.storePassword
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            // 是否混淆
            minifyEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 是否zip对齐
            zipAlignEnabled true
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release2.initWith(buildTypes.release)
        release2 {
            signingConfig signingConfigs.release2
            matchingFallbacks = ['release','debug']
        }


        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def productName = variant.productFlavors.get(1).name // flavor维度1的名字
                def channelName = variant.productFlavors.get(0).name // flavor维度2的名字
                def versionName = variant.versionName
                def buildType = variant.buildType.name

                if (outputFileName != null && outputFileName.endsWith('.apk')) {
                    // 输出apk名称为 weixin_gp_1.0_release_2021.01.01-11:59:59.apk
                    def apkName = "${productName}_${channelName}_${versionName}_${buildType}_${buildTime()}.apk"
                    outputFileName = apkName
                }else if (outputFileName != null && outputFileName.endsWith('.aab')){
                    // 输出aab名称为weixin_gp_1.0_release_2021.01.01-11:59:59.aab
                    def aabName = "${productName}_${channelName}_${versionName}_${buildType}_${buildTime()}.aab"
                    outputFileName = aabName
                }
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    dependenciesConfig.each {k,v -> implementation v}

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //基类库
    implementation project(':app_core')
}